#!perl

use strict;
use warnings;

use OrePAN2::Injector;
use OrePAN2::Indexer;
use Plack::Request;
use Plack::Runner;

my $app = sub {
    my $env = shift;
    my $req = Plack::Request->new($env);

    if ($req->method eq 'POST') {
        my $module = $req->param('module'); # can be a git repo.
        my $author = $req->param('author') || 'DUMMY';
        my $directory = './oreorepanpan';

        eval {
            my $injector = OrePAN2::Injector->new(
                directory => $directory,
                author    => $author,
            );
            $injector->inject($module);

            OrePAN2::Indexer->new(directory => $directory)->make_index(
                no_compress => 1,
            );
        };
        if (my $err = $@) {
            return [500, [], [$err.'']]
        }
    }
    return [200, [], ['OK']]
};

my $runner = Plack::Runner->new;
$runner->parse_options(@ARGV);
$runner->run($app);

__END__

=head1 NAME

orepan2-inject-server - OrePAN2 server

=head1 SYNOPSIS

    curl --data-urlencode 'module=git@github.com:Songmu/p5-App-RunCron.git' --data-urlencode 'author=SONGMU' http://example.com:5000/

=head1 DESCRIPTION

This is an application server. It accepts upload request.

=head1 TODO

Receive request from C<cpan-upload-http>.

=head1 AUTHOR

Songmu

